      SUBROUTINE XSECHD(E0,EP,SINSQ,ITYP,IHD,VW2,W2,W1,XSECTN,CSMOTT,
     >X)
C...
C...THIS FITS ARE FOR HYDROGEN AND DEUTERIUM.
C...USES ATWOOD'S RESONANCE FITS AND CALCULATE VW2 AS:
C...VW2=B*F2 WHERE B IS BACKGROUND WITH RESONANCES AND F2 IS
C...A UNIVERSAL FUNCTION TO THE DEEP INELASTIC
C... 
C...PARAMETER DESCRIPTION:
C...E0 IS THE ELECTRON INCIDENT ENERGY.                  (INPUT)
C...EP IS THE SCATTERED ELECTRON ENERGY.                 (INPUT)
C...SINSQ IS (SIN(THETA/2))**2.THETA IS THE SCATTERING
C...ANGLE                                                (INPUT)
C...ITYP CHOOSES WHICH FIT IS GOING TO BE USED.          (INPUT)
C...IHD= 1 FOR HYDROGEN,2 FOR DEUTERIUM, -1 for neutron.
C...ONLY VALID IF THE FIT IS AVAILABLE FOR H OR D2       (INPUT)
C...VW2,W2,W1 AND XSECTN ARE THE FITTED STRUCTURE
C...FUNCTIONS AND THE FITTED CROSS SECTION RESPECTEVILY. (OUTPUT)
C...XMOTT IS THE MOTT CROSS SECTION                      (OUTPUT)
C...X IS THE PARTON MOMENTUM FRACTION, X = QSQ/2MV       (OUTPUT)
C...
      implicit none

      real*8 e0,ep,sinsq,vw2,w2,w1,xsectn,csmott,x
      integer*4 ityp,ihd
      real*8 cossq,tansq,q2,v,tpm,vsq,w,pmsq
      real*8 alphax,omegap,sp,univ,bres,r

      REAL*8 C(24),CF(11),cn(11),CD(24),CFD(11),F2,B,QSQ,WW
      DATA PMSQ/.880324/,TPM/1.876512/
      DATA R/.18/,ALPHAX/137.0388/
C...
C...::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
C...CROSS SECTION FIT FOR HYDROGEN OR DEUTERIUM
C...USES ATWOOD'S RESONANCE FIT WITH COEFFICIENTS FROM FITTING
C...E87,E49A,E49B.COEFFICIENTS SUPLIED BY ARIE BODEK FOR E139
C...FINAL HYDROGEN COEFFS. FROM FITTING WITH NOTUSE=TRUE,ISLIDE=FALSE
C...CHISQ=3995,NO. OF POINTS=2533,FREE PARAMS=28,CHISQ/D.F.=1.59
C...THIS DATA PROVIDED BY ARIE BODEK FOR E139 (10/3/83)
C...
C...C(24)=HYDROGEN COEFFICIENTS FOR B (BACKGROUND AND RESONANCE
C... TERMS)
C...
      DATA   C(1) / 0.10741163D 01/,  C(2) / 0.75531124D 00/,
     *       C(3) / 0.33506491D 01/,  C(4) / 0.17447015D 01/,
     *       C(5) / 0.35102405D 01/,  C(6) / 0.10400040D 01/,
     *       C(7) / 0.12299128D 01/,  C(8) / 0.10625394D 00/,
     *       C(9) / 0.48132786D 00/,  C(10)/ 0.15101467D 01/,
     *       C(11)/ 0.81661975D-01/,  C(12)/ 0.65587179D 00/,
     *       C(13)/ 0.17176216D 01/,  C(14)/ 0.12551987D 00/,
     *       C(15)/ 0.74733793D 00/,  C(16)/ 0.19538129D 01/,
     *       C(17)/ 0.19891522D 00/,  C(18)/-0.17498537D 00/,
     *       C(19)/ 0.96701919D-02/,  C(20)/-0.35256748D-01/,
     *       C(21)/ 0.35185207D 01/,  C(22)/-0.59993696D 00/,
     *       C(23)/ 0.47615828D 01/,  C(24)/ 0.41167589D 00/
C...
C...CF(11)=HYDROGEN COEFFICIENTS FOR F2 (UNIVERSAL FUNCTION)
C...OMEGAW FIT
C...
      DATA CF(1) / 0.25615498D 00/,  CF(2) / 0.21784826D 01/,
     *     CF(3) / 0.89783738D 00/,  CF(4) /-0.67162450D 01/,
     *     CF(5) / 0.37557472D 01/,  CF(6) / 0.16421119D 01/,
     *     CF(7) / 0.37635747D 00/,  CF(8) / 0.93825625D 00/,
     *     CF(9) / 0.10000000D 01/,  CF(10)/ 0.0           /,
     *     CF(11)/ 0.50000000D 00/
C...
C...CN(11)=NEUTRON COEFFICIENTS FOR F2 (UNIVERSAL FUNCTION)
C...
      DATA CN(1) / 0.06400000D 00/,  CN(2) / 0.22500000D 00/,
     *     CN(3) / 0.41060000D 01/,  CN(4) /-0.70790000D 01/,
     *     CN(5) / 0.30550000D 01/,  CN(6) / 0.16421119D 01/,
     *     CN(7) / 0.37635747D 00/,  CN(8) / 0.93825625D 00/,
     *     CN(9) / 0.10000000D 01/,  CN(10)/ 0.0           /,
     *     CN(11)/ 0.50000000D 00/
C...
C...
C... FINAL DEUTERIUM COEFFS FROM FITTING WITH NOTUSE=TRUE,ISLIDE=FALSE
C... CHISQ=4456,NO. OF POINTS 2303,FREE PERAMS=26,CHISQ/D.F.=1.96
C... THIS DATA PROVIDED BY ARIE BODEK FOR E139 (10/3/83)
C...
C... CD(24)=DEUTERIUM COEFFICIENTS FOR B (BACKGROUND AND RESONAN
C... TERMS)
C...
      DATA  CD(1) / 0.10521935D 01/, CD(2) / 0.76111537D 00/,
     *      CD(3) / 0.41469897D 01/, CD(4) / 0.14218146D 01/,
     *      CD(5) / 0.37119053D 01/, CD(6) / 0.74847487D 00/,
     *      CD(7) / 0.12399742D 01/, CD(8) / 0.12114898D 00/,
     *      CD(9) / 0.11497852D-01/, CD(10)/ 0.14772317D 01/,
     *      CD(11)/ 0.69579815D-02/, CD(12)/ 0.12662466D 00/,
     *      CD(13)/ 0.15233427D 01/, CD(14)/ 0.84094736D-01/,
     *      CD(15)/ 0.74733793D 00/, CD(16)/ 0.19538129D 01/,
     *      CD(17)/ 0.19891522D 00/, CD(18)/-0.24480414D 00/,
     *      CD(19)/ 0.14502846D-01/, CD(20)/-0.35256748D-01/,
     *      CD(21)/ 0.35185207D 01/, CD(22)/-0.21261862D 00/,
     *      CD(23)/ 0.69690531D 01/, CD(24)/ 0.40314293D 00/
C...
C...CFD(11) ARE DEUTERIUM COEFFICIENTS FOR F2 (UNIVERSAL FUNCTION)
C....OMEGAW FIT
C...
      DATA CFD(1) / 0.47708776D 00/, CFD(2) / 0.21601918D 01/,
     *     CFD(3) / 0.36273894D 01/, CFD(4) /-0.10470367D 02/,
     *     CFD(5) / 0.49271691D 01/, CFD(6) / 0.15120763D 01/,
     *     CFD(7) / 0.35114723D 00/, CFD(8) / 0.93825625D 00/,
     *     CFD(9) / 0.10000000D 01/, CFD(10)/ 0.0           /,
     *     CFD(11)/ 0.50000000D 00/
C...::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
C...
C...COMPUTE SOME KINEMATIC QUANTITIES
C...
      COSSQ=1.0-SINSQ
C...
C...CHECK THAT DIVISION BY ZERO DOES NOT HAPPEN
C...
      IF((E0.EQ.0.).OR.(EP.EQ.0.).OR.(SINSQ.EQ.0.).OR.(
     * COSSQ.EQ.0.)) GOTO 4
C...
      TANSQ=SINSQ/COSSQ
      Q2=4.0*E0*EP*SINSQ
      V=E0-EP
	if (v.le.0.) goto 4
      X= Q2/(TPM*V)
C	if (x.ge.2.0) goto 4        
	if (x.ge.1.0) goto 4
      VSQ=V*V
      W=SQRT(PMSQ+TPM*V-Q2)
      CSMOTT=(19732./(2.0*ALPHAX*E0*SINSQ))**2*COSSQ
      OMEGAP=TPM*V/Q2+PMSQ/Q2
C...
C...OVERCOME RISK OF UNDERFLOW IN THE EXPONENTIATION
C...
      OMEGAP=MIN(20.d0,OMEGAP)
C...
      SP=1.0-EXP(-7.7*(OMEGAP-1.0))
      WW=W
      QSQ=Q2
 
C...CHOOSE THE FIT
      GOTO (1) ITYP
 
C...THIS IS ATWOOD'S TYPE OF FIT WITH COEFFICIENTS OBTAINED FROM
C...FITTING E87,E49A AND E49B.
C...CHOOSE IF HYDROGEN OR DEUTERIUM FIT
 
1     IF(IHD.EQ.2) GOTO 2

C...GET UNIVERSAL AND RESONANCE FIT FOR HYDROGEN
C...
      if (ihd.eq.1) then
        UNIV=F2(WW,QSQ,CF)
      else if (ihd.eq.-1) then
        UNIV=F2(WW,QSQ,CN)
      else
	write(6,*) 'Invalid Target: IHD=',ihd
      endif
      BRES=B(WW,QSQ,C)
      GOTO 3
C...
C...GET UNIVERSAL AND RESONANCE FIT FOR DEUTERIUM
C...
   2  UNIV=F2(WW,QSQ,CFD)/SP
      BRES=B(WW,QSQ,CD)
C...
C...COMPUTE VW2,W2,W1,AND THE CROSS SECTION
C...
   3  VW2=UNIV*BRES
      W2=VW2/V
      W1=(1.0+VSQ/QSQ)/(V*(1.0+R))*VW2
      XSECTN=(W2+2.0*TANSQ*W1)*CSMOTT

      RETURN
   4  XSECTN=0.
      RETURN
      END
C...
C...
      DOUBLE PRECISION FUNCTION F2(WM,QSQ,CF)
C...
C... UNIVERSAL FUNCTION FOR ATWOOD'S FIT
C...
      IMPLICIT NONE

      real*8 cf(11)
      real*8 pm2,pmsq
      real*8 wm,qsq,v,omega,xx,xpx,omegaw,arg

c      DIMENSION CF(11)
      DATA PM2/1.876512D0/,PMSQ/.880324D0/
C...
C... OMEGAW FIT...NO PHOTO-PRODUCTION COUPLING
C...
      V=(WM**2+QSQ-PMSQ)/PM2
      OMEGA=2.D0*CF(8)*V/QSQ
      XX=1.D0/OMEGA
      XPX=CF(9)+CF(10)*(XX-CF(11))**2
      OMEGAW=(2.D0*CF(8)*V+CF(6))/(QSQ+CF(7))
      ARG=1.D0-1.D0/OMEGAW
      F2=OMEGAW/OMEGA*ARG**3*(CF(1)+CF(2)*ARG+
     > CF(3)*ARG**2+CF(4)*ARG**3+CF(5)*ARG**4)
      F2=F2*XPX
      RETURN
      END
 
 
      DOUBLE PRECISION FUNCTION B(WM,QSQ,C)
C...
C...BACKGROUND AND RESONANCE CONTRIBUTION FOR ATWOOD'S FIT
C...
      IMPLICIT NONE

      integer nres,nbkg
      integer i,j,k,index
      real*8 c(24),lspin(4)
      real*8 pmsq,pm2,pm
      real*8 wm,qsq,wsq,omega,x,xpx,piemsq
      real*8 b1,eb1,b2,eb2,bbkg,bres,ressum
      real*8 ram,rma,rwd,qstarn,qstaro
      real*8 term,termo,gamres,brwig,res

C      DIMENSION C(24)
C      DIMENSION LSPIN(4)
      DATA LSPIN/1,2,3,2/
      DATA PMSQ/.880324D0/,PM2/1.876512D0/,PM/.938256D0/
      DATA NRES/4/,NBKG/5/
C...
C...KINEMATICS
C...
      WSQ=WM**2
      OMEGA=1.D0+(WSQ-PMSQ)/QSQ
      X=1.D0/OMEGA
      XPX=C(22)+C(23)*(X-C(24))**2
      PIEMSQ=(C(1)-PM)**2
C...
C...COLLECT BACKGROUND TERMS
C...CHECK FOR EXPONENTIAL UNDERFLOWS BEFORE THEY HAPPEN
C...
      B1=DMAX1(0.D0,(WM-C(1)))/(WM-C(1))*C(2)
      EB1=C(3)*(WM-C(1))
      IF(EB1.GT.25.0D0) GO TO 1
      B1=B1*(1.0D0-DEXP(-EB1))
   1  B2=DMAX1(0.D0,(WM-C(4)))/(WM-C(4))*(1.D0-C(2))
      EB2=C(5)*(WSQ-C(4)**2)
      IF(EB2.GT.25.0D0) GOTO 2
      B2=B2*(1.0D0-DEXP(-EB2))
   2  CONTINUE
      BBKG=B1+B2
      BRES=C(2)+B2
C...
C...COLLECT RES. CONTRIBUTION
C...
      RESSUM=0.D0
      DO 30 I=1,NRES
      INDEX=(I-1)*3+1+NBKG
      RAM=C(INDEX)
      IF(I.EQ.1) RAM=C(INDEX)+C(18)*QSQ+C(19)*QSQ**2
      RMA=C(INDEX+1)
      IF(I.EQ.3) RMA=RMA*(1.D0+C(20)/(1.D0+C(21)*QSQ))
      RWD=C(INDEX+2)
      QSTARN=DSQRT(DMAX1(0.D0,((WSQ+PMSQ-PIEMSQ)/(2.D0*WM))**2-PMSQ))
      QSTARO=DSQRT(DMAX1(0.D0,((RMA**2-PMSQ+PIEMSQ)/
     > (2.D0*RMA))**2-PIEMSQ))
      IF(QSTARO.EQ.0.D0) GOTO 40
      TERM=6.08974D0*QSTARN
      TERMO=6.08974D0*QSTARO
      J=2*LSPIN(I)
      K=J+1
      GAMRES=RWD*(TERM/TERMO)**K*(1.D0+TERMO**J)/(1.D0+TERM**J)
      GAMRES=GAMRES/2.D0
      BRWIG=GAMRES/((WM-RMA)**2+GAMRES**2)/3.1415926D0
      RES= RAM*BRWIG/PM2
      GOTO 30
 40   RES=0.D0
 30   RESSUM=RESSUM+RES
C...
C...FORM VW2/F2
C...
      B=BBKG*(1.D0+(1.D0-BBKG)*XPX)+RESSUM*(1.D0-BRES)
      RETURN
      END
